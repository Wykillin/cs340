#!/bin/csh

# Shar and mail program files to your IN, TA(s), and yourself.

set cn   = 340                         # course no
set ls   = ( 1 2 3 4 )                 # section nos

set IN1  = alhoori                     # login id of section 1 IN
set IN2  = zhou                        # login id of section 2 IN
set IN3  = cs340                       # login id of section 3 IN
set IN4  = zhou                        # login id of section 4 IN
set INs  = ( $IN1 $IN2 $IN3 $IN4 )     # login ids of all INs

set TA1  = z1776873                    # login id of TA1
set TA2  = z1780715                    # login id of TA2
set TA3  = z1778291                    # login id of TA3
set TA4  = z1789593                    # login id of TA4

set TA_1 = ( $TA1 )                    # login ids of section 1 TAs
set TA_2 = ( $TA2 )                    # login ids of section 2 TAs
set TA_3 = ( $TA3 )                    # login ids of section 3 TAs
set TA_4 = ( $TA4 )                    # login ids of section 4 TAs

set TAs  = ( $TA_1 $TA_2 $TA_3 $TA_4 ) # login ids of all TAs
@ nTAs   = 1                           # tot no of TAs in section

echo ''
echo '*********************************************************************'
echo '* WARNING: Do not use this program to mail notes to your instructor *'
echo '*          Doing so may result in loss of your program \!\!           *'
echo '*********************************************************************'
echo ''

echo -n 'Enter program num of your assignment: '
set pn1 = $<

set pn2 = `echo $pn1 | perl -e '$_ = <STDIN>; print $1 if /^\D*(\d+)$/'`
if ( $pn2 == "" ) then
    echo "*** ${pn1}: invalid program number ***"
    echo '*** submission of your program failed ***'
    echo ''; exit 1
else
    @ pn = $pn2
endif

# Source and header files of program can either be entered
# from keyboard or they can be supplied on UNIX command line.

if ( $#argv == 0 ) then
    echo 'Enter source (and header) files of your program:'
    set flist = $<; set flist = ( $flist )
else
    set flist = ( $* )
endif

# Check if each submitted file is source/header file
# and if it does exist. If not, exit program.

@ n = 1; @ err_flag = 0; @ first = 1
while ( $n <= $#flist )
    if ( ! -e $flist[$n] ) then
        if ( $first ) echo ""; @ first = 0
        echo "*** ${flist[$n]}: not exist ***"
        @ err_flag = 1
    else if ( $flist[$n] !~ .*.h  && $flist[$n] !~ *.h &&\
        $flist[$n] !~ .*.cc && $flist[$n] !~ *.cc ) then
        if ( $first ) echo ""; @ first = 0
        echo "*** ${flist[$n]}: not source/header file ***"
        @ err_flag = 1
    endif
    @ n++
end

if ( $err_flag ) then
    echo '*** submission of your program failed ***'
    echo ''; exit 2
endif

echo ''
echo '*******************************'
echo '* Section 1: Hamed Alhoori    *'
echo '*    TA: Soukya Lokula        *'
echo '* Section 2: Jie Zhou         *'
echo '*    TA: Aditya Sabbineni     *'
echo '* Section 3: Ibrahim Onyuksel *'
echo '*    TA: Khaja Siddiqui       *'
echo '* Section 4: Jie Zhou         *'
echo '*    TA: Jyostna Ankam        *'
echo '*******************************'
echo ''

echo -n 'Enter your section num: '
set sn1 = $<

set sn2 = `echo $sn1 | perl -e '$_ = <STDIN>; print $1 if /^\D*(\d+)$/'`
if ( $sn2 == "" ) then
    echo "*** ${sn1}: invalid section number ***"
    echo '*** submission of your program failed ***'
    echo ''; exit 3
else
    @ sn = $sn2
endif

@ i = 1
while ( $i <= $#ls && $sn != $ls[$i] )
    @ i++
    if ( $i > $#ls ) then
        echo "*** ${sn}: no such section ***"
        echo '*** submission of your program failed ***'
        echo ''; exit 4
    endif
end

# Create IN mailbox for given section.

set IN = $INs[$i]-hw

# Create list of TA mailboxes for given section.

@ k = ( $i - 1 ) * $nTAs + 1
@ n = 1; set TA = ( )
while ( $n <= $nTAs )
    set TA = ( $TA $TAs[$k]-hw )
    @ k++; @ n++
end

set com = $0
set td = /tmp/$com:t.$$  # temp dir
mkdir $td

# Copy all source files of your assignment to temp dir, change
# to that dir, shar all these files in single batch, and then
# mail resulting shar'ed file to your IN's and TA(s)' program
# mailboxes, and if you don't have file .procmailrc in your home
# dir, also mail resulting file to yourself.

set SELF = $USER
if ( -e $HOME/.procmailrc ) set SELF = ""

/bin/cp $flist $td; cd $td
shar -fm * | mailx -s "${cn}: Program $pn" $IN $TA $SELF

# Change to your working dir and remove temp dir.
cd ..; /bin/rm -r $td
